# Contributor: anekos
# Maintainer: anekos
pkgname=chrysoberyl-git
_pkgname=chrysoberyl
pkgdesc="Viewer for (local/remote) (images|PDFs|images in archive)"
pkgver=0.1.0.888
pkgrel=1
arch=('x86_64')
conflicts=("chrysoberyl")
provides=("chrysoberyl")
url="https://github.com/anekos/chrysoberyl"
license=('GPL')
makedepends=('cargo' 'gendesk')
source=('git://github.com/anekos/chrysoberyl.git')
sha256sums=('SKIP')

prepare() {
  gendesk -f \
    --pkgname "$_pkgname" \
    --pkgdesc "$pkgdesc" \
    --genericname 'Image viewer' \
    --categories='Graphics;2DGraphics;Viewer;' \
    --mimetypes='image/bmp;image/gif;image/jpeg;image/jpg;image/pjpeg;image/png;image/tiff;image/x-bmp;image/x-pcx;image/x-png;image/x-portable-anymap;image/x-portable-bitmap;image/x-portable-graymap;image/x-portable-pixmap;image/x-tga;image/x-xbitmap;application/pdf' \
    --exec 'chrysoberyl %f'
}

pkgver() {
  cd $_pkgname
  echo $(grep '^version =' Cargo.toml|head -n1|cut -d\" -f2).$(git rev-list --count HEAD)
}

build() {
  cd $_pkgname
  cargo build --release
}

package() {
  cd $_pkgname
  install -D -m755 "$srcdir/$_pkgname/target/release/chrysoberyl" "$pkgdir/usr/bin/chrysoberyl"
  install -Dm644 "$srcdir/$_pkgname.desktop" "$pkgdir/usr/share/applications/$_pkgname.desktop"
  install -Dm644 -t "$pkgdir/usr/share/$_pkgname/script" "$srcdir/$_pkgname/res/script/"*
}

